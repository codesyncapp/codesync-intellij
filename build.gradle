import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// Copyright 2000-2020 JetBrains s.r.o. and other contributors. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.

plugins {
  id 'java'
  id("org.jetbrains.intellij") version "1.17.3"
  id("org.jetbrains.kotlin.jvm") version "2.0.0"
}

group 'com.codesync'
version '4.3.0'


compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"


java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

repositories {
  mavenCentral()
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
  version.set("2024.1")
}

buildSearchableOptions {
  enabled = false
}

// See https://plugins.jetbrains.com/docs/intellij/build-number-ranges.html#platformVersions
patchPluginXml {
  version = project.version
  sinceBuild = '203'
  untilBuild = '242.*'
}

dependencies {
  implementation files('libs/diff_match_patch.jar')
  implementation files('libs/gitignore-file-filter-1.0.0.jar')

  // https://mvnrepository.com/artifact/com.googlecode.json-simple/json-simple
  implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'

  // https://mvnrepository.com/artifact/javax.websocket/javax.websocket-api
  compileOnly group: 'javax.websocket', name: 'javax.websocket-api', version: '1.1'

  // https://mvnrepository.com/artifact/org.glassfish.tyrus.bundles/tyrus-standalone-client
  implementation group: 'org.glassfish.tyrus.bundles', name: 'tyrus-standalone-client', version: '2.0.0'

  // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

  // https://mvnrepository.com/artifact/software.amazon.awssdk/cloudwatch
  implementation ('software.amazon.awssdk:cloudwatch:2.16.98') {
    exclude group: 'org.slf4j'
  }
  // https://mvnrepository.com/artifact/software.amazon.awssdk/cloudwatch
  implementation ('software.amazon.awssdk:cloudwatchlogs:2.16.98') {
    exclude group: 'org.slf4j'
  }

  // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
  implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.40.0.0'

  implementation 'org.eclipse.jetty:jetty-server:9.4.3.v20170317'
  implementation 'org.eclipse.jetty:jetty-servlet:9.4.3.v20170317'

  // https://github.com/auth0/java-jwt
  implementation 'com.auth0:java-jwt:3.18.1'

  // https://commons.apache.org/proper/commons-text/dependency-info.html
  implementation 'org.apache.commons:commons-text:1.9'

  // https://mvnrepository.com/artifact/org.eclipse.jgit/org.eclipse.jgit
  implementation ('org.eclipse.jgit:org.eclipse.jgit:6.8.0.202311291450-r') {
    exclude group: 'org.slf4j'
  }

  // Configuration for tests.
  testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2')
  testImplementation('org.junit.jupiter:junit-jupiter-engine:5.4.2')
  testImplementation('org.mockito:mockito-core:4.6.1')
  testImplementation('org.mockito:mockito-inline:4.6.1')
}

test {
  // Enable JUnit 5 (Gradle 4.6+).
  useJUnitPlatform()

  // Always run tests, even when nothing changed.
  dependsOn 'cleanTest'

  // Show test results.
  testLogging {
    events "passed", "skipped", "failed"
  }
}

publishPlugin {
  token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}
